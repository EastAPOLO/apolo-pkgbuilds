pkgname=notepadqq-git
_pkgname=notepadqq
pkgver=1.4.8.r189.g1c830b55
pkgrel=1
pkgdesc="A Linux clone of Notepad++"
arch=('i686' 'x86_64')
url="https://notepadqq.com/"
license=('GPL3')
depends=('qt5-webengine' 'qt5-websockets' 'qt5-svg' 'hicolor-icon-theme' 'desktop-file-utils' 'uchardet')
makedepends=('git' 'qt5-tools')
optdepend=('nodejs: nodejs extensions support')
provides=('notepadqq')
conflicts=('notepadqq-bin' 'notepadqq' 'notepadqq-common' 'notepadqq-src')
install=${_pkgname}.install
sha1sums=('SKIP'
          'SKIP')
options=('!emptydirs')

source=("git://github.com/notepadqq/notepadqq.git"
        "git://github.com/notepadqq/CodeMirror.git")

pkgver() {
	cd "${_pkgname}"
	set -o pipefail
	git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${_pkgname}"

  git config submodule.src/editor/libs/codemirror.url "${srcdir}/CodeMirror"
  git submodule init
  git submodule update
}

build() {
	cd "${_pkgname}"
	
	qmake-qt5 PREFIX=/usr LRELEASE=/usr/bin/lrelease notepadqq.pro

	make
	
}

package() {
	cd "${_pkgname}"

	make INSTALL_ROOT="${pkgdir}" install
}
